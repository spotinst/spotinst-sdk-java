version = project.property("theVersion")
nexusURL = project.property("nexusURL")

group 'com.spotinst'
version version

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()

    maven { url "http://repo1.maven.org/maven2/" }
}
sourceSets {
    main {
        java {
            srcDirs= ["src/main/java"]
        }
        resources {
            srcDirs= ["src/main/resources"]
        }
    }
}

publishing {
       publications {
               maven(MavenPublication) {
                       from components.java
                       artifact sourcesJar
                       artifact javadocJar
               }
       }
       repositories {
               maven {
                       url nexusURL
               }
       }
}

dependencies {
    compile('org.apache.httpcomponents:httpclient:4.5.2',
            'com.fasterxml.jackson.core:jackson-core:2.8.5',
            'com.fasterxml.jackson.core:jackson-databind:2.8.5',
            'org.slf4j:slf4j-api:1.7.22',
            'org.slf4j:slf4j-simple:1.7.22',
            'org.apache.commons:commons-lang3:3.5',
            'org.apache.logging.log4j:log4j-core:2.7',
            'org.apache.logging.log4j:log4j-api:2.7')
    testCompile(
            'junit:junit:4.12',
            'org.mockito:mockito-all:1.10.19',
            'org.powermock:powermock-module-junit4:1.6.6',
            'org.powermock:powermock-api-mockito:1.6.6'
    )
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.getDestinationDir()
}

task fatJar(type: Jar) {
    classifier = 'all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task versionTxt() {
    doLast {
        new File("$projectDir/src/main/resources/version.txt").text = """Version:$version """
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives fatJar
}

task createPom << {
    pom {
        project {
            groupId 'com.spotinst'
            artifactId 'spotinst-sdk-java'
            version version
        }
    }.writeTo("pom.xml")
}

task buildZip(type: Zip) {
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtime
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport



buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'

build.dependsOn buildZip

shadowJar {
    baseName = 'spotinst-sdk-java'
    classifier = 'fatJar'
    version = version
}
